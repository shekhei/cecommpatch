[Vertex shader]

varying vec4 pixelPos;
varying vec3 norm;
varying vec3 eyeNormal;
varying vec4 vWorldPosition;
varying vec4 vAdjustPosition;
varying vec4 vLocalPosition;
varying vec2 texCoord0;
varying vec2 texCoord1;

uniform mat4 worldMatrix;
uniform mat4 invCameraMatrix;

void main()
{
	vLocalPosition = gl_Vertex;
	vWorldPosition = (gl_ModelViewMatrix * gl_Vertex);
	vAdjustPosition = (invCameraMatrix * gl_ModelViewMatrix) * gl_Vertex;
	gl_Position = gl_ModelViewProjectionMatrix * gl_Vertex;
	pixelPos = gl_Position;

	// Lighting vectors
	
	norm = gl_Normal;
	eyeNormal = gl_NormalMatrix * gl_Normal;
	
	texCoord0 = gl_MultiTexCoord0.xy;
	texCoord1 = gl_MultiTexCoord1.xy;
}

[Fragment shader]
#version 150

uniform sampler2D baseTex;
uniform sampler2D sideTex;
uniform sampler2D alphaTex;
uniform mat4 lightMatrix;
uniform mat4 invCameraMatrix;
uniform mat4 worldMatrix;

#define saturate(x) clamp(x,0.0,1.0)
#define lerp mix

uniform float ambient;

varying vec2 texCoord0;
varying vec2 texCoord1;

varying vec3 norm;
varying vec3 eyeNormal;
uniform vec3 lightVec;

varying vec4 pixelPos;
varying vec4 vWorldPosition;
varying vec4 vAdjustPosition;
varying vec4 vLocalPosition;

uniform float specPower;
uniform float specIntensity;

uniform vec4 lightColor;
uniform vec4 lightAmbient;

#include shadow_frag_terrain.inl
#include deferred.inl

vec4 piecewiseBlend(vec4 upperTex, vec4 lowerTex, float amount)
{
	const float blendRange = 0.1;
	vec4 baseTexel;
	
	amount *= 1.0 + blendRange;
	
	if (upperTex.a < amount - blendRange) 
	{
		baseTexel = upperTex;
	} 
	else if (upperTex.a > amount + blendRange) 
	{
		baseTexel = lowerTex;
	} 
	else 
	{
		float f = (upperTex.a - (amount - blendRange)) / (2.0 * blendRange);
		baseTexel = lerp(upperTex, lowerTex, f);	
	}	
	
	return baseTexel;
}

void main()
{	
	WriteOutPositionAndNormals();
	vec3 normal = normalize(norm);
	float scale = 0.019;
   
	vec4 vAdjustedPosition = vAdjustPosition * scale;
	vec4 vAdjustedPosition23 = vAdjustedPosition * 0.66667;
	vec4 vAdjustedPosition14 = vAdjustedPosition * 0.25;

	float tighten = 0.4679; 
	
	vec3 blendweights = abs(normal) - tighten;
	blendweights = max(blendweights, 0);
   
	float mXY = blendweights.z;
	float mXZ = blendweights.y; 
	float mYZ = blendweights.x; 
   
	float total = 1.0 / (mXY + mXZ + mYZ);
	mXZ *= total;
	
	float sideTotal = 1.0 / max((mXY + mYZ), 0.0001);
	mXY *= sideTotal;
	mYZ *= sideTotal;
   
   // need scale to be such that 8 units over is one texture
	
	vec4 cXZ = 0.5 * texture(baseTex, vec2(vAdjustedPosition.x, vAdjustedPosition.z)) 
	+ 0.5 * texture(baseTex, vec2(vAdjustedPosition14.x , vAdjustedPosition14.z));   
	vec4 cXY = texture(sideTex, vec2(vAdjustedPosition23.x, vAdjustedPosition23.y));
	vec4 cYZ = texture(sideTex, vec2(vAdjustedPosition23.z, vAdjustedPosition23.y));
	vec4 baseTexel = piecewiseBlend(cXZ, cXY * mXY + cYZ * mYZ, mXZ);
	
	// smoothen the result a bit, to avoid aliasing at shadow contact point
	// FIXME: Divide by W?

	vec4 lightCoords = lightMatrix * (invCameraMatrix * vWorldPosition);	
	vec4 light = saturate(saturate(PCFShadowMap(lightCoords)+0.25) * saturate(dot(eyeNormal, lightVec)) * lightColor + lightAmbient);
	
	gl_FragData[1] = baseTexel * light;
	gl_FragData[1].w = 1.0;

	gl_FragData[2].xyzw = vec4(0.0, 0.0, 0.0, 1.0);
	// I don't remember what goes into the other things.		
}
